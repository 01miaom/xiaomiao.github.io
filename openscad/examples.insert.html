
<pre id="example001_scad" class="hidden">

module example001()
{
  function r_from_dia(d) = d / 2;

  module rotcy(rot, r, h) {
    rotate(90, rot)
      cylinder(r = r, h = h, center = true);
  }

  difference() {
    sphere(r = r_from_dia(size));
    rotcy([0, 0, 0], cy_r, cy_h);
    rotcy([1, 0, 0], cy_r, cy_h);
    rotcy([0, 1, 0], cy_r, cy_h);
  }

  size = 50;
  hole = 25;

  cy_r = r_from_dia(hole);
  cy_h = r_from_dia(size * 2.5);
}

example001();
</pre>

<pre id="example002_scad" class="hidden">

module example002()
{
  intersection() {
    difference() {
      union() {
        cube([30, 30, 30], center = true);
        translate([0, 0, -25])
          cube([15, 15, 50], center = true);
      }
      union() {
        cube([50, 10, 10], center = true);
        cube([10, 50, 10], center = true);
        cube([10, 10, 50], center = true);
      }
    }
    translate([0, 0, 5])
      cylinder(h = 50, r1 = 20, r2 = 5, center = true);
  }
}

example002();
</pre>

<pre id="example003_scad" class="hidden">

module example003()
{
  difference() {
    union() {
      cube([30, 30, 30], center = true);
      cube([40, 15, 15], center = true);
      cube([15, 40, 15], center = true);
      cube([15, 15, 40], center = true);
    }
    union() {
      cube([50, 10, 10], center = true);
      cube([10, 50, 10], center = true);
      cube([10, 10, 50], center = true);
    }
  }
}

example003();
</pre>

<pre id="example004_scad" class="hidden">

module example004()
{
  difference() {
    cube(30, center = true);
    sphere(20, $fn=20);
  }
}

example004();
</pre>
  
<pre id="example005_scad" class="hidden">

module example005()
{
  translate([0, 0, -120]) {
    difference() {
      cylinder(h = 50, r = 100);
      translate([0, 0, 10]) cylinder(h = 50, r = 80);
      translate([100, 0, 35]) cube(50, center = true);
    }
    for (i = [0:5]) {
      echo(360*i/6, sin(360*i/6)*80, cos(360*i/6)*80);
      translate([sin(360*i/6)*80, cos(360*i/6)*80, 0 ])
        cylinder(h = 200, r=10);
    }
    translate([0, 0, 200])
      cylinder(h = 80, r1 = 120, r2 = 0);
  }
}

example005();
</pre>
  
<pre id="example006_scad" class="hidden">

module example006()
{
  module edgeprofile()
  {
    render(convexity = 2) difference() {
      cube([20, 20, 150], center = true);
      translate([-10, -10, 0])
        cylinder(h = 80, r = 10, center = true);
      translate([-10, -10, +40])
        sphere(r = 10);
      translate([-10, -10, -40])
        sphere(r = 10);
    }
  }

  difference()
  {
    cube(100, center = true);
    for (rot = [ [0, 0, 0], [1, 0, 0], [0, 1, 0] ]) {
      rotate(90, rot)
        for (p = [[+1, +1, 0], [-1, +1, 90], [-1, -1, 180], [+1, -1, 270]]) {
          translate([ p[0]*50, p[1]*50, 0 ])
            rotate(p[2], [0, 0, 1])
              edgeprofile();
        }
    }
    for (i = [
      [ 0, 0, [ [0, 0] ] ],
      [ 90, 0, [ [-20, -20], [+20, +20] ] ],
      [ 180, 0, [ [-20, -25], [-20, 0], [-20, +25], [+20, -25], [+20, 0], [+20, +25] ] ],
      [ 270, 0, [ [0, 0], [-25, -25], [+25, -25], [-25, +25], [+25, +25] ] ],
      [ 0, 90, [ [-25, -25], [0, 0], [+25, +25] ] ],
      [ 0, -90, [ [-25, -25], [+25, -25], [-25, +25], [+25, +25] ] ]
    ]) {
      rotate(i[0], [0, 0, 1]) rotate(i[1], [1, 0, 0]) translate([0, -50, 0])
        for (j = i[2])
          translate([j[0], 0, j[1]]) sphere(10);
    }
  }
}

example006();
</pre>
  
<pre id="example007_scad" class="hidden">

module cutout()
{
    intersection()
    {
      rotate(90, [1, 0, 0])
      translate([0, 0, -50])
        linear_extrude(height = 100, convexity = 1)
          import(file = "example007.dxf", layer = "cutout1");
      
      rotate(90, [0, 0, 1])
      rotate(90, [1, 0, 0])
      translate([0, 0, -50])
        linear_extrude(height = 100, convexity = 2)
          import(file = "example007.dxf", layer = "cutout2");
    }
}

module clip()
{
  difference() {
    // NB! We have to use the deprecated module here since the "dorn"
                // layer contains an open polyline, which is not yet supported
                // by the import() module.
    rotate_extrude(
      file = "example007.dxf",
      layer="dorn",
      convexity = 3);
    for (r = [0, 90])
      rotate(r, [0, 0, 1])
        cutout();
  }
}

module cutview()
{
  difference()
  {
    difference()
    {
      translate([0, 0, -10])
        clip();

      rotate(20, [0, 0, 1])
        rotate(-20, [0, 1, 0])
        translate([18, 0, 0])
        cube(30, center = true);
    }

    # render(convexity = 5) intersection()
    {
      translate([0, 0, -10])
        clip();
    
      rotate(20, [0, 0, 1])
        rotate(-20, [0, 1, 0])
        translate([18, 0, 0])
        cube(30, center = true);
    }
  }
}

translate([0, 0, -10])
  clip();

// cutview();
</pre>
  
<pre id="example008_scad" class="hidden">

difference()
{
  intersection()
  {
    translate([ -25, -25, -25])
    linear_extrude(height = 50, convexity = 3)
      import(file = "example008.dxf", layer = "G");
    
    rotate(90, [1, 0, 0])
    translate([ -25, -125, -25])
    linear_extrude(height = 50, convexity = 3)
      import(file = "example008.dxf", layer = "E");
    
    rotate(90, [0, 1, 0])
    translate([ -125, -125, -25])
    linear_extrude(height = 50, convexity = 3)
      import(file = "example008.dxf", layer = "B");
  }

  intersection()
  {
    translate([ -125, -25, -26])
    linear_extrude(height = 52, convexity = 1)
      import(file = "example008.dxf", layer = "X");

    rotate(90, [0, 1, 0])
    translate([ -125, -25, -26])
    linear_extrude(height = 52, convexity = 1)
      import(file = "example008.dxf", layer = "X");
  }
}
</pre>
  
<pre id="example009_scad" class="hidden">

bodywidth = dxf_dim(file = "example009.dxf", name = "bodywidth");
fanwidth = dxf_dim(file = "example009.dxf", name = "fanwidth");
platewidth = dxf_dim(file = "example009.dxf", name = "platewidth");
fan_side_center = dxf_cross(file = "example009.dxf", layer = "fan_side_center");
fanrot = dxf_dim(file = "example009.dxf", name = "fanrot");

% linear_extrude(height = bodywidth, center = true, convexity = 10)
  import(file = "example009.dxf", layer = "body");

% for (z = [+(bodywidth/2 + platewidth/2),
    -(bodywidth/2 + platewidth/2)])
{
  translate([0, 0, z])
  linear_extrude(height = platewidth, center = true, convexity = 10)
    import(file = "example009.dxf", layer = "plate");
}

intersection()
{
  linear_extrude(height = fanwidth, center = true, convexity = 10, twist = -fanrot)
    import(file = "example009.dxf", layer = "fan_top");
    
  // NB! We have to use the deprecated module here since the "fan_side"
  // layer contains an open polyline, which is not yet supported
  // by the import() module.
  rotate_extrude(file = "example009.dxf", layer = "fan_side",
    origin = fan_side_center, convexity = 10);
}


</pre>
  
<pre id="example010_scad" class="hidden">

// example010.dat generated using octave:
//   d = (sin(1:0.2:10)' * cos(1:0.2:10)) * 10;
//   save("example010.dat", "d");

intersection()
{
  surface(file = "example010.dat",
    center = true, convexity = 5);
  
  rotate(45, [0, 0, 1])
  surface(file = "example010.dat",
    center = true, convexity = 5);
}

</pre>

<pre id="example011_scad" class="hidden">
polyhedron(
  points=[ 
    [10,10,0],
    [10,-10,0],
    [-10,-10,0],
    [-10,10,0],
    [0,0,10]  
  ],
  triangles=[ 
    [0,1,4],
    [1,2,4],
    [2,3,4],
    [3,0,4],
    [1,0,3],
    [2,1,3] 
  ]
 );

</pre>

<pre id="example012_scad" class="hidden">

/* Please copy example012.stl to your dropbox folder to be able to show this example. */

// example012.stl is Mblock.stl, (c) 2009 Will Langford
// licensed under the Creative Commons - GNU GPL license.
// http://www.thingiverse.com/thing:753

difference()
{
  sphere(20);
  
  translate([ -2.92, 0.5, +20 ]) rotate([180, 0, 180])
      import("example012.stl", convexity = 5);
}

</pre>

<pre id="example013_scad" class="hidden">
intersection()
{
  linear_extrude(height = 100, center = true, convexity= 3)
    import(file = "example013.dxf");
  rotate([0, 90, 0])
  linear_extrude(height = 100, center = true, convexity= 3)
    import(file = "example013.dxf");
  rotate([90, 0, 0])
  linear_extrude(height = 100, center = true, convexity= 3)
    import(file = "example013.dxf");
}
</pre>

<pre id="example014_scad" class="hidden">

intersection_for(i = [
      [0, 0, 0],
      [10, 20, 300],
      [200, 40, 57],
      [20, 88, 57]
    ])
  rotate(i) cube([100, 20, 20], center = true);

</pre>

<pre id="example015_scad" class="hidden">

module shape()
{
  difference()
  {
    translate([ -35, -35 ]) intersection()
    {
      union() {
        difference() {
          square(100, true);
          square(50, true);
        }
        translate([ 50, 50 ])
          square(15, true);
      }
      rotate(45) translate([ 0, -15 ]) square([ 100, 30 ]);
    }
  
    rotate(-45) scale([ 0.7, 1.3 ]) circle(5);
  }
  
  import(file = "example009.dxf", layer = "body",
      convexity = 6, scale=2);
}

// linear_extrude(convexity = 10, center = true)
shape();


</pre>

<pre id="example016_scad" class="hidden">

// example016.stl is derived from Mblock.stl
// (c) 2009 Will Langford licensed under
// the Creative Commons - GNU GPL license.
// http://www.thingiverse.com/thing:753
//
// Jonas Pfeil converted the file to binary
// STL and duplicated its content.

module blk1() {
  cube([ 65, 28, 28 ], center = true);
}

module blk2() {
  difference() {
    translate([ 0, 0, 7.5 ])
      cube([ 60, 28, 14 ], center = true);
    cube([ 8, 32, 32 ], center = true);
  }
}

module chop() {
  translate([ -14, 0, 0 ])
    import(file = "example016.stl", convexity = 12);
}

difference() {
  blk1();
  for (alpha = [0, 90, 180, 270]) {
    rotate(alpha, [ 1, 0, 0]) render(convexity = 12)
      difference() {
        blk2();
        chop();
      }
  }
}

</pre>

<pre id="example017_scad" class="hidden">

// To render the DXF file from the command line:
// openscad -x example017.dxf -D'mode="parts"' example017.scad

// mode = "parts";
// mode = "exploded";
mode = "assembled";

thickness = 6;
locklen1 = 15;
locklen2 = 10;
boltlen = 15;
midhole = 10;
inner1_to_inner2 = 50;
total_height = 80;

module shape_tripod()
{
  x1 = 0;
  x2 = x1 + thickness;
  x3 = x2 + locklen1;
  x4 = x3 + thickness;
  x5 = x4 + inner1_to_inner2;
  x6 = x5 - thickness;
  x7 = x6 - locklen2;
  x8 = x7 - thickness;
  x9 = x8 - thickness;
  x10 = x9 - thickness;
  
  y1 = 0;
  y2 = y1 + thickness;
  y3 = y2 + thickness;
  y4 = y3 + thickness;
  y5 = y3 + total_height - 3*thickness;
  y6 = y5 + thickness;
  
  union()
  {
    difference() {
      polygon([
        [ x1, y2 ], [ x2, y2 ],
        [ x2, y1 ], [ x3, y1 ], [ x3, y2 ],
        [ x4, y2 ], [ x4, y1 ], [ x5, y1 ],
        [ x5 + thickness, y3 ], [ x5, y4 ],
        [ x5, y5 ],
        [ x6, y5 ], [ x6, y6 ], [ x7, y6 ], [ x7, y5 ], [ x8, y5 ],
        [ x8, y6 ], [ x9, y5 ],
        [ x9, y4 ], [ x10, y3 ],
        [ x2, y3 ]
      ]);
      translate([ x10, y4 ]) circle(thickness);
      translate([ x5 + thickness, y4 ]) circle(thickness);
    }
  
    translate([ x5, y1 ])
    square([ boltlen - thickness, thickness*2 ]);
  
    translate([ x5 + boltlen - thickness, y2 ]) circle(thickness);
  
    translate([ x2, y2 ]) intersection() {
      circle(thickness);
      translate([ -thickness*2, 0 ]) square(thickness*2);
    }
  
    translate([ x8, y5 ]) intersection() {
      circle(thickness);
      translate([ -thickness*2, 0 ]) square(thickness*2);
    }
  }
}

module shape_inner_disc()
{
  difference() {
    circle(midhole + boltlen + 2*thickness + locklen2);
    for (alpha = [ 0, 120, 240 ])
      rotate(alpha) translate([ 0, midhole + boltlen + thickness + locklen2/2 ]) square([ thickness, locklen2 ], true);
    circle(midhole + boltlen);
  }
}

module shape_outer_disc()
{
  difference() {
    circle(midhole + boltlen + inner1_to_inner2 + 2*thickness + locklen1);
    for (alpha = [ 0, 120, 240 ])
      rotate(alpha) translate([ 0, midhole + boltlen + inner1_to_inner2 + thickness + locklen1/2 ]) square([ thickness, locklen1 ], true);
    circle(midhole + boltlen + inner1_to_inner2);
  }
}

module parts()
{
  tripod_x_off = locklen1 - locklen2 + inner1_to_inner2;
  tripod_y_off = max(midhole + boltlen + inner1_to_inner2 + 4*thickness + locklen1, total_height);

  shape_inner_disc();
  shape_outer_disc();

  for (s = [ [1,1], [-1,1], [1,-1] ])
    scale(s) translate([ tripod_x_off, -tripod_y_off ]) shape_tripod();
}

module exploded()
{
  translate([ 0, 0, total_height + 2*thickness ]) linear_extrude(height = thickness, convexity = 4) shape_inner_disc();
  linear_extrude(height = thickness, convexity = 4) shape_outer_disc();

  color([ 0.7, 0.7, 1 ]) for (alpha = [ 0, 120, 240 ])
    rotate(alpha) translate([ 0, thickness*2 + locklen1 + inner1_to_inner2 + boltlen + midhole, 1.5*thickness ])
      rotate([ 90, 0, -90 ]) linear_extrude(height = thickness, convexity = 10, center = true) shape_tripod();
}

module bottle()
{
  r = boltlen + midhole;
  h = total_height - thickness*2;

  rotate_extrude(convexity = 2)
  {
    square([ r, h ]);

    translate([ 0, h ])
    intersection() {
      square([ r, r ]);
      scale([ 1, 0.7 ]) circle(r);
    }

    translate([ 0, h+r ])
    intersection() {
      translate([ 0, -r/2 ]) square([ r/2, r ]);
      circle(r/2);
    }
  }
}

module assembled()
{
  translate([ 0, 0, total_height - thickness ]) linear_extrude(height = thickness, convexity = 4) shape_inner_disc();
  linear_extrude(height = thickness, convexity = 4) shape_outer_disc();

  color([ 0.7, 0.7, 1 ]) for (alpha = [ 0, 120, 240 ])
    rotate(alpha) translate([ 0, thickness*2 + locklen1 + inner1_to_inner2 + boltlen + midhole, 0 ])
      rotate([ 90, 0, -90 ]) linear_extrude(height = thickness, convexity = 10, center = true) shape_tripod();

  % translate([ 0, 0, thickness*2]) bottle();
}

if (mode == "parts")
  parts();

if (mode == "exploded")
  exploded();

if (mode == "assembled")
  assembled();
</pre>


<pre id="example018_scad" class="hidden">

module step(len, mod)
{
  for (i = [0:$children-1])
    translate([ len*(i - ($children-1)/2), 0, 0 ]) child((i+mod) % $children);
}

for (i = [1:4])
{
  translate([0, -250+i*100, 0]) step(100, i)
  {
    sphere(30);
    cube(60, true);
    cylinder(r = 30, h = 50, center = true);
  
    union() {
      cube(45, true);
      rotate([45, 0, 0]) cube(50, true);
      rotate([0, 45, 0]) cube(50, true);
      rotate([0, 0, 45]) cube(50, true);
    }
  }
}

</pre>


<pre id="example019_scad" class="hidden">

function get_cylinder_h(p) = lookup(p, [
    [ -200, 5 ],
    [ -50, 20 ],
    [ -20, 18 ],
    [ +80, 25 ],
    [ +150, 2 ]
  ]);

for (i = [-100:5:+100]) {
  // echo(i, get_cylinder_h(i));
  translate([ i, 0, -30 ]) cylinder(r1 = 6, r2 = 2, h = get_cylinder_h(i)*3);
}


</pre>


<pre id="example020_scad" class="hidden">

module screw(type = 2, r1 = 15, r2 = 20, n = 7, h = 100, t = 8)
{
  linear_extrude(height = h, twist = 360*t/n, convexity = t)
  difference() {
    circle(r2);
    for (i = [0:n-1]) {
        if (type == 1) rotate(i*360/n) polygon([
            [ 2*r2, 0 ],
            [ r2, 0 ],
            [ r1*cos(180/n), r1*sin(180/n) ],
            [ r2*cos(360/n), r2*sin(360/n) ],
            [ 2*r2*cos(360/n), 2*r2*sin(360/n) ],
        ]);
        if (type == 2) rotate(i*360/n) polygon([
            [ 2*r2, 0 ],
            [ r2, 0 ],
            [ r1*cos(90/n), r1*sin(90/n) ],
            [ r1*cos(180/n), r1*sin(180/n) ],
            [ r2*cos(270/n), r2*sin(270/n) ],
            [ 2*r2*cos(270/n), 2*r2*sin(270/n) ],
        ]);
    }
  }
}

module nut(type = 2, r1 = 16, r2 = 21, r3 = 30, s = 6, n = 7, h = 100/5, t = 8/5)
{
  difference() {
    cylinder($fn = s, r = r3, h = h);
    translate([ 0, 0, -h/2 ]) screw(type, r1, r2, n, h*2, t*2);
  }
}

module spring(r1 = 100, r2 = 10, h = 100, hr = 12)
{
  stepsize = 1/16;
  module segment(i1, i2) {
    alpha1 = i1 * 360*r2/hr;
    alpha2 = i2 * 360*r2/hr;
    len1 = sin(acos(i1*2-1))*r2;
    len2 = sin(acos(i2*2-1))*r2;
    if (len1 &lt; 0.01)
      polygon([
        [ cos(alpha1)*r1, sin(alpha1)*r1 ],
        [ cos(alpha2)*(r1-len2), sin(alpha2)*(r1-len2) ],
        [ cos(alpha2)*(r1+len2), sin(alpha2)*(r1+len2) ]
      ]);
    if (len2 &lt; 0.01)
      polygon([
        [ cos(alpha1)*(r1+len1), sin(alpha1)*(r1+len1) ],
        [ cos(alpha1)*(r1-len1), sin(alpha1)*(r1-len1) ],
        [ cos(alpha2)*r1, sin(alpha2)*r1 ],
      ]);
    if (len1 >= 0.01 &amp;&amp; len2 >= 0.01)
      polygon([
        [ cos(alpha1)*(r1+len1), sin(alpha1)*(r1+len1) ],
        [ cos(alpha1)*(r1-len1), sin(alpha1)*(r1-len1) ],
        [ cos(alpha2)*(r1-len2), sin(alpha2)*(r1-len2) ],
        [ cos(alpha2)*(r1+len2), sin(alpha2)*(r1+len2) ]
      ]);
  }
  linear_extrude(height = 100, twist = 180*h/hr,
      $fn = (hr/r2)/stepsize, convexity = 5) {
    for (i = [ stepsize : stepsize : 1+stepsize/2 ])
      segment(i-stepsize, min(i, 1));
  }
}

translate([ -30, 0, 0 ])
screw();

translate([ 30, 0, 0 ])
nut();

spring();

</pre>


<pre id="example021_scad" class="hidden">

module thing()
{
  $fa = 30;
  difference() {
    sphere(r = 25);
    cylinder(h = 62.5, r1 = 12.5, r2 = 6.25, center = true);
    rotate(90, [ 1, 0, 0 ]) cylinder(h = 62.5,
        r1 = 12.5, r2 = 6.25, center = true);
    rotate(90, [ 0, 1, 0 ]) cylinder(h = 62.5,
        r1 = 12.5, r2 = 6.25, center = true);
  }
}

module demo_proj()
{
  linear_extrude(center = true, height = 0.5) projection(cut = false) thing();
  % thing();
}

module demo_cut()
{
  for (i=[-20:5:+20]) {
     rotate(-30, [ 1, 1, 0 ]) translate([ 0, 0, -i ])
      linear_extrude(center = true, height = 0.5) projection(cut = true)
        translate([ 0, 0, i ]) rotate(+30, [ 1, 1, 0 ]) thing();
  }
  % thing();
}

translate([ -30, 0, 0 ]) demo_proj();
translate([ +30, 0, 0 ]) demo_cut();

</pre>


<pre id="example022_scad" class="hidden">
  // size is a vector [w, h, d]
module roundedBox(size, radius, sidesonly)
{
  rot = [ [0,0,0], [90,0,90], [90,90,0] ];
  if (sidesonly) {
    cube(size - [2*radius,0,0], true);
    cube(size - [0,2*radius,0], true);
    for (x = [radius-size[0]/2, -radius+size[0]/2],
         y = [radius-size[1]/2, -radius+size[1]/2]) {
      translate([x,y,0]) cylinder(r=radius, h=size[2], center=true);
    }
  }
  else {
    cube([size[0], size[1]-radius*2, size[2]-radius*2], center=true);
    cube([size[0]-radius*2, size[1], size[2]-radius*2], center=true);
    cube([size[0]-radius*2, size[1]-radius*2, size[2]], center=true);

    for (axis = [0:2]) {
      for (x = [radius-size[axis]/2, -radius+size[axis]/2],
          y = [radius-size[(axis+1)%3]/2, -radius+size[(axis+1)%3]/2]) {
        rotate(rot[axis]) 
          translate([x,y,0]) 
          cylinder(h=size[(axis+2)%3]-2*radius, r=radius, center=true);
      }
    }
    for (x = [radius-size[0]/2, -radius+size[0]/2],
        y = [radius-size[1]/2, -radius+size[1]/2],
        z = [radius-size[2]/2, -radius+size[2]/2]) {
      translate([x,y,z]) sphere(radius);
    }
  }
}

translate([-15,0,0])roundedBox([20,30,40], 5, true);
translate([15,0,0]) roundedBox([20,30,40], 5, false);


</pre>


<pre id="example023_scad" class="hidden">
// Example combining MCAD/fonts.scad with search() function.

use &lt;MCAD/fonts.scad&gt;

thisFont=8bit_polyfont();
x_shift=thisFont[0][0];
y_shift=thisFont[0][1];

hours=["one","two","three","four","five","six","seven","eight","nine","ten","eleven","twelve"];

module clock_hour_words(word_offset=20.0,word_height=2.0) {
  for(i=[0:(len(hours)-1)]) assign( hourHandAngle=(i+1)*360/len(hours), theseIndicies=search(hours[i],thisFont[2],1,1) ) {
    rotate(90-hourHandAngle) translate([word_offset,0])
      for( j=[0:(len(theseIndicies)-1)] ) translate([j*x_shift,-y_shift/2]) {
        linear_extrude(height=word_height) polygon(points=thisFont[2][theseIndicies[j]][6][0],paths=thisFont[2][theseIndicies[j]][6][1]);
      }
  }
}

clock_hour_words(word_offset=16.0,word_height=5.0);

</pre>